plugins {
    id 'org.springframework.boot' version '2.7.4'
	id 'io.spring.dependency-management' version '1.0.14.RELEASE'
	id 'java'
}

repositories {
    mavenCentral()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.6.11'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'io.rest-assured:rest-assured:4.5.1'
    
    intTestImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    intTestImplementation 'io.rest-assured:rest-assured:4.5.1'
}

group = 'hands.on.testkube'
version = '1.0.0'
sourceCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
}

// integrationTest.dependsOn test
check.dependsOn integrationTest

tasks.withType(Test) {
    useJUnitPlatform()
}

tasks.named("bootBuildImage") {
	imageName = "${project.name}:${project.version}"
}
